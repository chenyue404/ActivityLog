plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

android {
    namespace 'com.chenyue404.activitylog'
    compileSdkVersion 33

    defaultConfig {
        applicationId "com.chenyue404.activitylog"
        minSdkVersion 24
        targetSdkVersion 33
        versionCode 3
        versionName "2.0"
    }

    File certificationConfFile = file("certification.properties")
    Properties certificationConf = new Properties()
    certificationConf.load(new FileInputStream(certificationConfFile))

    signingConfigs {
        my {
            storeFile file(certificationConf['storeFile'])
            storePassword certificationConf['storePassword']
            keyAlias certificationConf['keyAlias']
            keyPassword certificationConf['keyPassword']
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.my
        }
        debug {
            signingConfig signingConfigs.my
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            if (variant.buildType.name == 'release') {
                def fileName = "ActivityLog_${defaultConfig.versionCode}_${defaultConfig.versionName}.apk"
                outputFileName = fileName
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '11'
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.10.0'
    compileOnly 'de.robv.android.xposed:api:82'
//    compileOnly 'de.robv.android.xposed:api:82:sources'
    implementation("com.google.code.gson:gson:2.9.0")
    implementation 'androidx.recyclerview:recyclerview:1.3.0'
}